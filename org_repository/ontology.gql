insert

organization sub entity
	has name
	plays owner
	plays employer;

platform sub entity
	has name
	has description
	has url
	plays owned-by
	plays platform-has-environment;

environment sub entity
	has name
	plays environment-has-platform
	plays environment-has-tool
	plays environment-has-dataset;

tool sub entity
	has name
	plays source
	plays destination
	plays tool-has-environment
	plays ancestor
	plays descendant;
	
dataset sub entity
	has name
	has description
	has created
	has updated
	has format
	has location
	has url
	has database
	has table
	plays owned-by
	plays source
	plays destination
	plays dataset-has-environment
	plays ancestor
	plays descendant;

person sub entity
	has name
	plays employee
	plays owner;

#Relations
employment sub relation
	relates employer
	relates employee;

ownership sub relation
	relates owner
	relates owned-by;

process sub relation
	has name
	relates source
	relates destination;

platform-environment sub relation
	relates platform-has-environment
	relates environment-has-platform;

environment-tool sub relation
	relates environment-has-tool
	relates tool-has-environment;

environment-dataset sub relation
	relates environment-has-dataset
	relates dataset-has-environment;

#lineage sub relation
#	relates source
#	relates destination;
ancestry sub relation
	relates ancestor
	relates descendant;

#Roles 
employee sub role;
employer sub role;
owner sub role;
owned-by sub role;
source sub role;
destination sub role;
platform-has-environment sub role;
environment-has-platform sub role;
tool-has-environment sub role;
environment-has-tool sub role;
dataset-has-environment sub role;
environment-has-dataset sub role;
ancestor sub role;
descendant sub role;

#Resources
name sub resource datatype string;
description sub resource datatype string;
created sub resource datatype string;
updated sub resource datatype string;
format sub resource datatype string;
location sub resource datatype string;
url sub resource datatype string;
database sub resource datatype string;
table sub resource datatype string;


#Rules
#$R2 isa inference-rule,
#when {
#    (source: $s, destination: $d) isa process;
#    (source: $d, destination: $d2) isa process;
#},
#then {
#    (source: $s, destination: $d2) isa lineage;
#};

$ancestor isa inference-rule 
when {
(source: $s, destination: $d) isa process;
} then {
(ancestor: $s, descendant: $d) isa ancestry; };

$transitive-ancestor isa inference-rule
when {
(ancestor: $a, descendant: $b) isa ancestry;
(ancestor: $b, descendant: $c) isa ancestry; 
} then {
(ancestor: $a, descendant: $c) isa ancestry; };
